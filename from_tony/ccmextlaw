awk ' # IR, Optical, and UV Extinction Law (Cardelli, Clayton, & Mathis 1989, ApJ, 345, 245)
BEGIN {
        printf("Enter wavelength of interest in angstroms : ")
}

$1 ~ /^[0-9]+$/ {
        wl = $1
        # Exit if wavelength is negative.
        if (wl < 0.) {
                printf("\nWavelength must be non-negative !")
                exit
        }
        x = 1./(wl/1.e4)
        # Out of range
        if ( x>10 || x<0.3 ) {
                printf("\nCannot compute in the entered wavelength range")
                exit
        }
        # Infrared
        if ( x>=0.3 && x<1.1 ) {
                ax = 0.574*x^1.61
                bx = -0.527*x^1.61
        }        
        # Optical/Near-Infrared
        if ( x>=1.1 && x<3.3 ) {
                y = x - 1.82
                ax = 1 + 0.17699*y - 0.50447*y^2 - 0.02427*y^3 + 0.72085*y^4 + 0.01979*y^5 - 0.77530*y^6 + 0.32999*y^7
                bx = 1.41338*y + 2.28305*y^2 + 1.07233*y^3 - 5.38434*y^4 - 0.62251*y^5 + 5.30260*y^6 - 2.09002*y^7
        }
        # Ultraviolet
        if ( x>=3.3 && x<8 ) {
                if (x>=5.9 && x<8) {
                        fax = -0.04473*(x-5.9)^2 - 0.009779*(x-5.9)^3
                        fbx = 0.2130*(x-5.9)^2 + 0.1207*(x-5.9)^3
                }
                if (x<5.9) {
                        fax = 0.
                        fbx = 0.
                }
                ax = 1.752 - 0.316*x - 0.104/((x-4.67)^2 + 0.341) + fax
                bx = -3.090 + 1.825*x + 1.206/((x-4.62)^2 + 0.263) + fbx
        }
        # Far-Ultraviolet
        if ( x>=8 && x<=10 ) {
                ax = -1.073 - 0.628*(x-8) + 0.137*(x-8)^2 - 0.070*(x-8)^3
                bx = 13.670 + 4.257*(x-8) - 0.420*(x-8)^2 + 0.374*(x-8)^3
        }
        printf("\nx = %6.2f",x)
        printf("\na(x) = %8.4f",ax)
        printf("\nb(x) = %8.4f",bx)
        printf("\nA(wl)/A(V) = %6.3f\n\n",ax+bx/3.1)
        exit
}

{ printf("\nInvalid entry.  Enter a number: ")
}' -
